Import('env')

#
# Need to make a copy because SCons uses the environment
# at it's final state, so StGermain ends up depending on
# StgDomain, etc.
#

env = env.Clone()

#
# Inside each project we will be accessing headers without the
# project name as a prefix, so we need to let SCons know how to
# find those headers.
#

env.Append(CPPPATH=env['build_dir'] + '/include/libUnderworldPy')

libs = ['libUnderworldPy'] + env.get('LIBS', []) 

#
# Keep a list of all the objects we build so we can make a library
# afterwards.
#

objs = []
suites = []
tst_exp = []
tst_input = []

#
# Process each directory uniformly.
#

dirs = Split('.')
for d in dirs:

    # Need the module name, which is just the directory.
    mod_name = env['ESCAPE']('"' + ''.join(d.split('/')) + '"')
    cpp_defs = env.get('CPPDEFINES', [])

    # Setup where to look for files.
    src_dir = d
    inc_dir = 'include/libUnderworldPy/' + d
    tst_dir = d + '/tests'
    tst_exp_dir = tst_dir + '/expected'
    tst_input_dir = tst_dir + '/input'
    tst_install_dir = 'tests/libUnderworldPy/' + d

    # Install the headers and '.def' files.
    hdrs = env.Install(inc_dir, Glob(src_dir + '/*.h'))

    # Build our source files.
    srcs = Glob(src_dir + '/*.c')
    objs += env.SharedObject(srcs, CPPDEFINES=cpp_defs)

    # Build any test suites we might find.
    suites += env.Object(Glob(tst_dir + '/*Suite.c'))

    # Install any test expected and input files
    tst_exp += env.Install(tst_install_dir + '/expected', Glob(tst_exp_dir + '/*'))
    tst_input += env.Install(tst_install_dir + '/input', Glob(tst_input_dir + '/*'))

# Need to install headers from liblibUnderworld.
env.Install('include/libUnderworldPy', Glob('libUnderworldPy/*.h'))

#
# Build shared library.
#

if env['shared_libs']:
    env.SharedLibrary('lib/libUnderworldPy', objs)


# if on osx, create symlink for python modules
import os
if env['SHLIBSUFFIX'] == '.dylib':
  destLink = os.path.join('..', env["build_dir"],'lib', 'libUnderworldPy.so')
  sourceFile = os.path.join('..', env["build_dir"],'lib', 'libUnderworldPy.dylib')
  def SymLink(target, source, env):
    os.symlink(os.path.abspath(str(source[0])), os.path.abspath(str(target[0])))
  env.Command(destLink, sourceFile, SymLink)
